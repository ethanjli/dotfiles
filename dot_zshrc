# .zshrc

# Define utilities

valid_command() {
  command="$1"
  command -v "$command" &> /dev/null && "$command" --version &> /dev/null
}
among_paths() {
  directory="$1"
  paths="$2"
  [[ ":${paths}:" == *":${directory}:"* ]]
}
xdg_config="${XDG_CONFIG_HOME:-$HOME/.config}"
xdg_data_home="${XDG_DATA_HOME:-$HOME/.local/share}"

# Set up completions
fpath=(~/.config/zsh/completion $fpath)
autoload compinit
compinit

# Use bash completions
autoload bashcompinit
bashcompinit

# Fix keys and shortcuts
WORDCHARS='_'
bindkey "^[[3~" delete-char
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word
bindkey "^A" vi-beginning-of-line
bindkey "^E" vi-end-of-line
bindkey "^H" backward-kill-word
bindkey "^[[3;5~" kill-word

# Set up distrobox completions
for file in /usr/share/bash-completion/completions/distrobox*(.N); do
  source "$file"
done

# Integrate aqua
aqua_path="${AQUA_ROOT_DIR:-${xdg_data_home}/aquaproj-aqua}/bin"
if [ -d "$aqua_path" ] && ! among_paths "$aqua_path" "$PATH"; then
  export PATH="${aqua_path}:$PATH"
fi
if valid_command aqua; then
  aqua_configs="${xdg_config_home}/aquaproj-aqua"
  aqua_global_config="${aqua_configs}/aqua.yaml"
  if [ -f "$aqua_global_config" ] && ! among_paths "$aqua_global_config" "$AQUA_GLOBAL_CONFIG"; then
    export AQUA_GLOBAL_CONFIG="${aqua_global_config}:${AQUA_GLOBAL_CONFIG:-}"
  fi
  aqua_policy_config="${aqua_configs}/aqua-policy.yaml"
  if [ -f "$aqua_policy_config" ] && ! among_paths "$aqua_policy_config" "$AQUA_POLICY_CONFIG"; then
    export AQUA_POLICY_CONFIG="${aqua_policy_config}:${AQUA_POLICY_CONFIG:-}"
  fi
  aqua i -a
  source <(aqua completion zsh)
fi

# Integrate neovim
if valid_command nvim; then
  export EDITOR="$(which nvim)"
  export VISUAL="$(which nvim)"
  alias vim=nvim
else
  export EDITOR="$(which vim)"
  export VISUAL="$(which vim)"
fi

# Integrate marktext
function marktext {
  flatpak run com.github.marktext.marktext "$@"
}
compdef _files marktext

# Integrate wezterm
function wezterm {
  flatpak run org.wezfurlong.wezterm "$@"
}

# Integrate tere
if valid_command tere; then
  tere() {
    local result=$($HOME/.local/share/aquaproj-aqua/bin/tere --mouse=on "$@")
    [ -n "$result" ] && cd -- "$result"
  }
fi

# Integrate lf
if valid_command lf; then
  function lfcd {
    cd "$($HOME/.local/share/aquaproj-aqua/bin/lf -print-last-dir "$@")"
  }
fi

# Integrate cheat
if valid_command fzf; then
  export CHEAT_USE_FZF=true
fi

# Integrate ripgrep-all
if valid_command rga; then
  rga-fzf() {
    RG_PREFIX="rga --files-with-matches"
    local file
    file="$(
      FZF_DEFAULT_COMMAND="$RG_PREFIX '$1'" \
        fzf --sort --preview="[[ ! -z {} ]] && rga --pretty --context 5 {q} {}" \
          --phony -q "$1" \
          --bind "change:reload:$RG_PREFIX {q}" \
          --preview-window="70%:wrap"
    )" &&
    echo "opening $file" &&
    xdg-open "$file"
  }
fi

# Enable starship
if valid_command starship; then
  eval "$(starship init zsh)"
fi

# Enable direnv
if valid_command direnv; then
  eval "$(direnv hook zsh)"
fi

# Enable atuin
if valid_command atuin; then
  eval "$(atuin init zsh)"
fi

# Integrate zoxide
if valid_command zoxide; then
  eval "$(zoxide init zsh)"
fi
# TODO: include https://github.com/marlonrichert/zsh-autocomplete
