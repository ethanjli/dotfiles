# .zshrc

_zsh_compile() {
  local f
  for f; do
    zcompile -R -- "$f".zwc "$f"
  done
}

# Install plugins
_zsh_plugins_installation() {
  unset -f "$0"

  # Suppress printing when launching background jobs:
  setopt no_notify local_options
  setopt no_monitor local_options

  local xdg_config_home="${XDG_CONFIG_HOME:-$HOME/.config}"
  local zsh_configs="${xdg_config_home}/zsh"
  local zsh_plugins="${zsh_configs}/plugins"
  local file
  for file in ${zsh_plugins}/*.install.zsh(.N); do
    "$file" &
  done
  # TODO: add an installer and loader for https://github.com/marlonrichert/zsh-autocomplete
  wait
  jobs > /dev/null
}
_zsh_plugins_installation

# Perform partial integration of direnv before the instant prompt
_zsh_integrations_pre() {
  unset -f "$0"

  # Set env vars so that aqua will provide direnv if needed
  local xdg_config_home="${XDG_CONFIG_HOME:-$HOME/.config}"
  local bash_configs="${xdg_config_home}/bash"
  local bash_integrations="${bash_configs}/integrations"
  if [ -f "${bash_integrations}/1-aqua.sh" ]; then
    source "${bash_integrations}/1-aqua.sh"
  fi

  # Set up direnv for instant prompt
  if command -v direnv &> /dev/null; then
    eval "$(direnv export zsh)"
  fi
}
_zsh_integrations_pre

# Activate powerlevel10k instant prompt
_zsh_instant_prompt() {
  unset -f "$0"
  local xdg_cache_home="${XDG_CACHE_HOME:-$HOME/.cache}"

  if [[ -r "${xdg_cache_home}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${xdg_cache_home}/p10k-instant-prompt-${(%):-%n}.zsh"
  fi
}
_zsh_instant_prompt
# Note: after this line, nothing is allowed to use stdin!

# Set up completions
_zsh_completions() {
  unset -f "$0"

  # Add local completions
  local xdg_config_home="${XDG_CONFIG_HOME:-$HOME/.config}"
  local zsh_configs="${xdg_config_home}/zsh"
  fpath=("${zsh_configs}/completions" $fpath)

  # Add third-party completions
  local zsh_plugins="${zsh_configs}/plugins"
  local file
  for file in ${zsh_plugins}/*.completions.zsh(.N); do
    source "$file"
  done

  # Configure completion caching
  local xdg_cache_home="${XDG_CACHE_HOME:-$HOME/.cache}"
  local zsh_cache="${xdg_cache_home}/zsh"
  zstyle ':completion:*' cache-path "${zsh_cache}/zcompcache"

  # Add more completion configuration
  zmodload zsh/complist # must be loaded before compinit
  bindkey -M menuselect 'h' vi-backward-char
  bindkey -M menuselect 'k' vi-up-line-or-history
  bindkey -M menuselect 'l' vi-forward-char
  bindkey -M menuselect 'j' vi-down-line-or-history

  # Compile completions
  local zsh_compdump="${zsh_cache}/zcompdump"
  autoload -Uz compinit && compinit -d "$zsh_compdump"
  if ! [[ "${zsh_compdump}.zwc" -nt "$zsh_compdump" ]]; then
    _zsh_compile "$zsh_compdump"
  fi

  # Add bash completions
  autoload -Uz bashcompinit
  bashcompinit
}
_zsh_completions

unset -f _zsh_compile

# Load plugins
ZSH_AUTOSUGGEST_MANUAL_REBIND=1
_zsh_plugins_loading() {
  unset -f "$0"

  local xdg_config_home="${XDG_CONFIG_HOME:-$HOME/.config}"
  local zsh_configs="${xdg_config_home}/zsh"
  local zsh_plugins="${zsh_configs}/plugins"
  local file
  for file in ${zsh_plugins}/*.load.zsh(.N); do
    source "$file"
  done
}
_zsh_plugins_loading

# Set up integrations after instant prompt
_zsh_integrations_post() {
  unset -f "$0"

  local xdg_config_home="${XDG_CONFIG_HOME:-$HOME/.config}"
  local bash_configs="${xdg_config_home}/bash"
  local bash_integrations="${bash_configs}/integrations"
  local file
  for file in ${bash_integrations}/*.sh(.N); do
    source "$file"
  done

  local zsh_configs="${xdg_config_home}/zsh"
  local zsh_integrations="${zsh_configs}/integrations"
  for file in ${zsh_integrations}/*.zsh(.N); do
    source "$file"
  done
}
_zsh_integrations_post

